plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'io.dongvelop'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"


    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor('org.projectlombok:lombok:1.18.30')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Q타입 클래스 생성 경로 (@Deprecated)
//def generated = "$buildDir/generated/qclass"

// Q타입 클래스 생성 경로 (Replace Version)
// Reference : https://docs.gradle.org/current/userguide/upgrading_version_8.html#deprecations
Provider<Directory> generated = layout.buildDirectory.dir("generated/qclass")
generated.get().asFile
generated.map { it.asFile.path }

// QueryDSL QClass 파일 생성 위치 설정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// Java Source Set에 QueryDSL QClass 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

// Gradle Clean시 QClass 디렉토리(generated) 삭제
clean {
    delete file(generated)
}
